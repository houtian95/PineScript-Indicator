//@version=4
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Sine Weighted Moving Average script may be freely distributed under the terms of the GPL-3.0 license.
study("Sine Weighted Moving Average", shorttitle="SWMA", overlay=true)

length = input(title="Length", type=input.integer, defval=14)
src = input(title="Source", type=input.source, defval=close)
highlight = input(title="Highlight ?", type=input.bool, defval=true)

PI = 2 * asin(1)
sum = 0.0
weightSum = 0.0

for i = 0 to length - 1
	weight = sin((i + 1) * PI / (length + 1))
	sum := sum + nz(src[i]) * weight
	weightSum := weightSum + weight
	weightSum

swma = sum / weightSum

swmaColor = highlight ? swma > swma[1] ? color.green : color.red : #6d1e7f
plot(swma, title="SWMA", linewidth=2, color=swmaColor, transp=0)
