//@version=4
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Kaufman Adaptive Moving Average script may be freely distributed under the terms of the GPL-3.0 license.
study("Kaufman Adaptive Moving Average", shorttitle="KAMA", overlay=true)

length = input(title="Length", type=input.integer, defval=14)
fastLength = input(title="Fast EMA Length", type=input.integer, defval=2)
slowLength = input(title="Slow EMA Length", type=input.integer, defval=30)
src = input(title="Source", type=input.source, defval=close)
highlight = input(title="Highlight ?", type=input.bool, defval=true)

mom = abs(change(src, length))
volatility = sum(abs(change(src)), length)

// Efficiency Ratio
er = volatility != 0 ? mom / volatility : 0

fastAlpha = 2 / (fastLength + 1)
slowAlpha = 2 / (slowLength + 1)

alpha = pow(er * (fastAlpha - slowAlpha) + slowAlpha, 2)

kama = 0.0
kama := alpha * src + (1 - alpha) * nz(kama[1], src)

kamaColor = highlight ? kama > kama[1] ? color.green : color.red : #6d1e7f
plot(kama, title="KAMA", linewidth=2, color=kamaColor, transp=0)
