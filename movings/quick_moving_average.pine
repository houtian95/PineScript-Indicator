//@version=3
// Copyright (c) 2008-present, John McCormick
// Copyright (c) 2019-present, Alex Orekhov (everget)
// Quick Moving Average script may be freely distributed under the terms of the GPL-3.0 license.
study("Quick Moving Average", shorttitle="QMA", overlay=true)

length = input(title="Length", type=integer, minval=1, defval=14)
src = input(title="Source", type=source, defval=close)
highlightMovements = input(title="Highlight Movements ?", type=bool, defval=false)

peak = length / 3
num = 0.0
denom = 0.0

for i = 1 to length + 1
	mult = 0.0
	if i <= peak
		mult := i / peak
	else
		mult := (length + 1 - i) / (length + 1 - peak)
	num := num + src[i - 1] * mult
	denom := denom + mult

qma = denom != 0.0 ? num / denom : src

qmaColor = highlightMovements ? (qma > qma[1] ? green : red) : #6d1e7f
plot(qma, title="QMA", linewidth=2, color=qmaColor, transp=0)
