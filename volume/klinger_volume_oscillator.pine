//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Klinger Volume Oscillator script may be freely distributed under the terms of GPL-3.0 license.
study(title="Klinger Volume Oscillator", shorttitle="KVO", precision=0)

fastLength = input(title="Fast Length", type=integer, defval=34)
slowLength = input(title="Slow Length", type=integer, defval=55)
showSignal = input(title="Show Signal ?", type=bool, defval=true)
signalType = input(title="Signal Smoothing Type", defval="EMA", options=["EMA", "SMA"])
signalLength = input(title="Signal Smoothing Length", type=integer, defval=13)
showHistogram = input(title="Show Histogram ?", type=bool, defval=false)
highlightCrossovers = input(title="Highlight KVO/Signal Crossovers ?", type=bool, defval=true)
highlightZeroCrossovers = input(title="Highlight Zero Line Crossovers ?", type=bool, defval=true)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=false)

mom = change(hlc3)

trend = 0.0
trend := na(trend[1]) ? 0.0 : (mom > 0 ? 1 : mom < 0 ? -1 : trend[1])

// Daily Measurement
dm = high - low

// Cumulative Measurement
cm = 0.0
cm := na(cm[1]) ? 0.0 : (trend == trend[1] ? cm[1] + dm : dm + dm[1])

// Volume Force
vf = cm != 0 ? 100 * volume * trend * abs(2 * dm / cm - 1) : 0

kvo = ema(vf, fastLength) - ema(vf, slowLength)
signal = signalType == "EMA" ? ema(kvo, signalLength) : sma(kvo, signalLength)

hist = kvo - signal
histColor = hist >= 0 ? (hist[1] < hist ? #26A69A : #B2DFDB) : (hist[1] < hist ? #FFCDD2 : #EF5350)
plot(showHistogram ? hist : na, title="Histogram", style=columns, color=histColor, transp=0)
hline(0, title="Zero Level", linestyle=dotted)

trendColor = kvo > signal ? #0ebb23 : red

kvoColor = applyFilling ? trendColor : (kvo > 0 ? #0ebb23 : red)
signalColor = applyFilling ? trendColor : #512da8

kvoPlot = plot(kvo, title="KVO", linewidth=2, color=kvoColor, transp=0)
signalPlot = plot(showSignal ? signal : na, title="Signal", color=signalColor, transp=0)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(kvoPlot, signalPlot, title="Ribbon", color=fillColor, transp=70)

zeroCrossBgColor = highlightZeroCrossovers ? (kvo > 0 ? green : red) : transparent
bgcolor(zeroCrossBgColor, title="Background", transp=85)

plotshape(crossover(kvo, signal) and highlightCrossovers ? signal : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=20)
plotshape(crossunder(kvo, signal) and highlightCrossovers ? signal : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=20)
