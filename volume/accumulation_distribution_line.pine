//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Accumulation/Distribution Line script may be freely distributed under the terms of GPL-3.0 license.
study("Accumulation/Distribution Line", shorttitle="Accum/Dist", precision=0)

showSignal = input(title="Show Signal ?", type=bool, defval=true)
signalType = input(title="Signal Smoothing Type", defval="SMA", options=["EMA", "SMA"])
signalLength = input(title="Signal Smoothing Length", type=integer, defval=21)
highlightCrossovers = input(title="Highlight Crossovers ?", type=bool, defval=false)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=true)

signal = signalType == "EMA" ? ema(accdist, signalLength) : sma(accdist, signalLength)

trendColor = accdist > signal ? #0ebb23 : red

adColor = applyFilling ? trendColor : #0094ff
signalColor = applyFilling ? trendColor : #ff6a00

adPlot = plot(accdist, title="Accum/Dist", linewidth=2, color=adColor, transp=0)
signalPlot = plot(showSignal ? signal : na, title="Signal", color=signalColor, transp=0)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(adPlot, signalPlot, color=fillColor, transp=70)

plotshape(crossover(accdist, signal) and highlightCrossovers ? accdist : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=0)
plotshape(crossunder(accdist, signal) and highlightCrossovers ? accdist : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=0)
