//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Positive Volume Index script may be freely distributed under the terms of GPL-3.0 license.
study("Positive Volume Index", shorttitle="PVI")

initial = input(title="Initial Value", type=integer, defval=100)
signalType = input(title="Signal Smoothing Type", defval="EMA", options=["EMA", "SMA"])
signalLength = input(title="Signal Length", type=integer, defval=255)
highlightCrossovers = input(title="Highlight Crossovers ?", type=bool, defval=false)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=true)

pvi = 0.0
pvi := na(pvi[1]) ? initial : (change(volume) > 0 ? pvi[1] * close / close[1] : pvi[1])
signal = signalType == "EMA" ? ema(pvi, signalLength) : sma(pvi, signalLength)

trendColor = pvi > signal ? #0ebb23 : red

pviColor = applyFilling ? trendColor : #0094ff
signalColor = applyFilling ? trendColor : #ff6a00

pviPlot = plot(pvi, title="PVI", color=pviColor, transp=0)
signalPlot = plot(signal, title="Signal", color=signalColor, transp=0)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(pviPlot, signalPlot, color=fillColor, transp=70)

plotshape(crossover(pvi, signal) and highlightCrossovers ? pvi : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=0)
plotshape(crossunder(pvi, signal) and highlightCrossovers ? pvi : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=0)
