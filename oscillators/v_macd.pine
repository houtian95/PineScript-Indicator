//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// V-MACD script may be freely distributed under the terms of GPL-3.0 license.
study("V-MACD")

fastLength = input(title="Fast Length", type=integer, defval=12)
slowLength = input(title="Slow Length", type=integer, defval=26)
signalLength = input(title="Signal Length", type=integer, defval=9)
showHistogram = input(title="Show Histogram ?", type=bool, defval=true)
highlightCrossovers = input(title="Highlight V-MACD/Signal Crossovers ?", type=bool, defval=false)
highlightZeroCrossovers = input(title="Highlight Zero Line Crossovers ?", type=bool, defval=false)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=false)

[vmacd, signal, hist] = macd(volume, fastLength, slowLength, signalLength)

histColor = hist >= 0 ? (hist[1] < hist ? #26A69A : #B2DFDB) : (hist[1] < hist ? #FFCDD2 : #EF5350)

plot(showHistogram ? hist : na, title="Histogram", style=columns, color=histColor, transp=0)
hline(0, title="Zero Line", linestyle=dotted)

trendColor = vmacd > signal ? #0ebb23 : red

vmacdColor = applyFilling ? trendColor : #0094ff
signalColor = applyFilling ? trendColor : #ff6a00

vmacdPlot = plot(vmacd, title="V-MACD", color=vmacdColor, transp=0)
signalPlot = plot(signal, title="Signal", color=signalColor, transp=0)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(vmacdPlot, signalPlot, color=fillColor, transp=70)

zeroCrossBgColor = highlightZeroCrossovers ? (vmacd > 0 ? green : red) : transparent
bgcolor(zeroCrossBgColor, transp=90)

plotshape(highlightCrossovers and crossover(vmacd, signal) ? vmacd : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=0)
plotshape(highlightCrossovers and crossunder(vmacd, signal) ? vmacd : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=0)
