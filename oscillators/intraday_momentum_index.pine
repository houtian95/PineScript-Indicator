//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Intraday Momentum Index script may be freely distributed under the terms of the GPL-3.0 license.
study("Intraday Momentum Index", shorttitle="IMI")

length = input(title="Length", type=integer, minval=1, defval=14)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=bool, defval=true)
src = input(title="Source", type=source, defval=close)

gain = close > open ? close - open : 0
loss = close > open ? 0 : open - close

upSum = sum(gain, length)
downSum = sum(loss, length)

imi = 100 * upSum / (upSum + downSum)

obLevel = 70
osLevel = 30

imiColor = imi > obLevel ? #0ebb23 : imi < osLevel ? #ff0000 : #f4b77d
plot(imi, title="IMI", linewidth=2, color=imiColor, transp=0)

transparent = color(white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=dotted)
hline(50, title="Middle Level", linestyle=dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=purple, transp=95)

obFillColor = imi > obLevel and highlightBreakouts ? green : transparent
osFillColor = imi < osLevel and highlightBreakouts ? red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)
