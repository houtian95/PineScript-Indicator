//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Rapid RSI script may be freely distributed under the terms of the GPL-3.0 license.
study("Rapid RSI", shorttitle="RRSI")

length = input(title="Length", type=integer, minval=1, defval=14)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=bool, defval=true)
src = input(title="Source", type=source, defval=close)

upSum = sum(max(change(src), 0), length)
downSum = sum(max(-change(src), 0), length)

rrsi = downSum == 0 ? 100 : upSum == 0 ? 0 : 100 - (100 / (1 + upSum / downSum))

obLevel = 70
osLevel = 30

rrsiColor = rrsi > obLevel ? #0ebb23 : rrsi < osLevel ? #ff0000 : #f4b77d
plot(rrsi, title="RRSI", linewidth=2, color=rrsiColor, transp=0)

transparent = color(white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=dotted)
hline(50, title="Middle Level", linestyle=dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=purple, transp=95)

obFillColor = rrsi > obLevel and highlightBreakouts ? green : transparent
osFillColor = rrsi < osLevel and highlightBreakouts ? red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)
