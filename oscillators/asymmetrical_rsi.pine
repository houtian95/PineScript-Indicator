//@version=4
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Asymmetrical RSI script may be freely distributed under the terms of the GPL-3.0 license.
study("Asymmetrical RSI", shorttitle="ARSI")

length = input(title="Length", type=input.integer, defval=14)
obLevel = input(title="Overbought Level", type=input.integer, defval=70)
osLevel = input(title="Oversold Level", type=input.integer, defval=30)
src = input(title="Source", type=input.source, defval=close)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=input.bool, defval=true)

roc = roc(src, 1)

upCount = sum(roc >= 0 ? 1 : 0, length)
upAlpha = 1 / upCount

upSum = 0.0
upSum := upAlpha * (roc >= 0 ? roc : 0) + (1 - upAlpha) * nz(upSum[1], upSum)

downCount = length - upCount
downAlpha = 1 / downCount

downSum = 0.0
downSum := downAlpha * (roc >= 0 ? 0 : abs(roc)) + (1 - downAlpha) * nz(downSum[1], downSum)

arsi = fixnan(100 * upSum / (upSum + downSum))

arsiColor = arsi > obLevel ? #0ebb23 : arsi < osLevel ? #ff0000 : #f4b77d
plot(arsi, title="ARSI", linewidth=2, color=arsiColor, transp=0)

transparent = color.new(color.white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=hline.style_dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=hline.style_dotted)
hline(50, title="Middle Level", linestyle=hline.style_dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=hline.style_dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=hline.style_dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=color.purple, transp=95)

obFillColor = arsi > obLevel and highlightBreakouts ? color.green : transparent
osFillColor = arsi < osLevel and highlightBreakouts ? color.red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)
