//@version=3
// Copyright (c) 2019-present, Alex Orekhov (everget)
// Dynamic Momentum Index script may be freely distributed under the terms of the GPL-3.0 license.
study("Dynamic Momentum Index", shorttitle="DMI")

rsiLength = input(title="Initial RSI Length", type=integer, minval=1, defval=14)
stdevLength = input(title="Standard Deviation Length", type=integer, minval=1, defval=5)
maLength = input(title="Smoothing Length", type=integer, minval=1, defval=10)
lower = input(title="Lower Bound", type=integer, defval=5)
upper = input(title="Upper Bound", type=integer, defval=30)
obLevel = input(title="Overbought Level", type=integer, defval=70)
osLevel = input(title="Oversold Level", type=integer, defval=30)
src = input(title="Source", type=source, defval=close)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=bool, defval=true)

stdev = stdev(src, stdevLength)
V = stdev / sma(stdev, maLength)

T = round(rsiLength / V)
T := T > upper ? upper : T < lower ? lower : T

// Wilder's Smoothing (Running Moving Average)
_rma(src, length) =>
    out = 0.0
    out := ((length - 1) * nz(out[1]) + src) / length

_rsi(src, length) =>
	upSum = _rma(max(change(src), 0), length)
	downSum = _rma(-min(change(src), 0), length)
	rsi = downSum == 0
		 ? 100
		 : upSum == 0
			 ? 0
			 : 100 - (100 / (1 + upSum / downSum))
	rsi

dmi = _rsi(src, T)

dmiColor = dmi > obLevel ? #0ebb23 : dmi < osLevel ? red : #f4b77d
plot(dmi, title="DMI", linewidth=2, color=dmiColor, transp=0)

transparent = color(white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=dotted)
hline(50, title="Middle Level", linestyle=dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=purple, transp=95)

obFillColor = dmi > obLevel and highlightBreakouts ? green : transparent
osFillColor = dmi < osLevel and highlightBreakouts ? red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)
