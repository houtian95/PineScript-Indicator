//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// VW-MACD script may be freely distributed under the terms of GPL-3.0 license.
study("VW-MACD")

fastLength = input(title="Fast Length", type=integer, defval=12)
slowLength = input(title="Slow Length", type=integer, defval=26)
signalLength = input(title="Signal Length", type=integer, defval=9)
showHistogram = input(title="Show Histogram ?", type=bool, defval=true)
highlightCrossovers = input(title="Highlight VW-MACD/Signal Crossovers ?", type=bool, defval=false)
highlightZeroCrossovers = input(title="Highlight Zero Line Crossovers ?", type=bool, defval=false)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=false)

vwmacd = vwma(close, fastLength) - vwma(close, slowLength)
signal = ema(vwmacd, signalLength)
hist = vwmacd - signal

histColor = hist >= 0 ? (hist[1] < hist ? #26A69A : #B2DFDB) : (hist[1] < hist ? #FFCDD2 : #EF5350)

plot(showHistogram ? hist : na, title="Histogram", style=columns, color=histColor, transp=0)
hline(0, title="Zero Line", linestyle=dotted)

trendColor = vwmacd > signal ? #0ebb23 : red

vwmacdColor = applyFilling ? trendColor : #0094ff
signalColor = applyFilling ? trendColor : #ff6a00

vwmacdPlot = plot(vwmacd, title="VW-MACD", color=vwmacdColor, transp=0)
signalPlot = plot(signal, title="Signal", color=signalColor, transp=0)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(vwmacdPlot, signalPlot, color=fillColor, transp=70)

zeroCrossBgColor = highlightZeroCrossovers ? (vwmacd > 0 ? green : red) : transparent
bgcolor(zeroCrossBgColor, transp=90)

plotshape(highlightCrossovers and crossover(vwmacd, signal) ? vwmacd : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=0)
plotshape(highlightCrossovers and crossunder(vwmacd, signal) ? vwmacd : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=0)
