//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Relative Volatility Index script may be freely distributed under the terms of the GPL-3.0 license.
study("Relative Volatility Index", shorttitle="RVI")

stdevLength = input(title="Standard Deviation Length", type=integer, minval=1, defval=10)
stdevType = input(title="Standard Deviation Type", defval="Population", options=["Population", "Sample"])
smoothLength = input(title="Smoothing Length", type=integer, minval=1, defval=14)
obLevel = input(title="Overbought Level", type=integer, defval=60)
osLevel = input(title="Oversold Level", type=integer, defval=40)
useRefined = input(title="Use Refined Version of RVI ?", type=bool, defval=true)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=bool, defval=true)
src = input(title="Source", type=source, defval=close)

sampleStdev(src, length) =>
	dev = src - sma(src, length)
	variance = sum(pow(dev, 2), length) / (length - 1)
	sqrt(variance)

selectedStdev = stdevType == "Population"
	 ? stdev(src, stdevLength)
	 : sampleStdev(src, stdevLength)

// Original version (1993)
rviOriginal(src) =>
	upSum = ema(change(src) >= 0 ? selectedStdev : 0, smoothLength)
	downSum = ema(change(src) >= 0 ? 0 : selectedStdev, smoothLength)
	100 * upSum / (upSum + downSum)

// Refined version (1995)
rvi = useRefined
	 ? avg(rviOriginal(high), rviOriginal(low))
	 : rviOriginal(src)

rviColor = rvi > obLevel ? #0ebb23 : rvi < osLevel ? #ff0000 : #f4b77d
plot(rvi, title="RVI", linewidth=2, color=rviColor, transp=0)

transparent = color(white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=dotted)
hline(50, title="Middle Level", linestyle=dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=purple, transp=95)

obFillColor = rvi > obLevel and highlightBreakouts ? green : transparent
osFillColor = rvi < osLevel and highlightBreakouts ? red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)
